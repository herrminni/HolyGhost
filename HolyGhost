#!/usr/bin/perl -w

# FEATURES TODO
# casper return code 139 ?
# www/results with .htaccess
#
# try to use only core perl modules
# pattern extraction
#
# catch:
# PhantomJS has crashed. Please read the crash reporting guide at https://github.com/ariya/phantomjs/wiki/Crash-Reporting and file a bug report at https://github.com/ariya/phantomjs/issues/new with the crash dump file attached: /tmp/074dd849-d003-d32b-6ffea8ae-06266fb7.dmp

# Usage:
sub PrintUsage {
    print <<USAGE;
Copyright (c) 2013-2015 Michael Kraus

This file is part of HolyGhost.

HolyGhost is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

HolyGhost is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.

This software uses phantomjs and casperjs. Make sure both programs are
installed and within your path.

Usage: $0 Opions:
    --test              File containing testcase (REQUIRED). The file name (without the suffix .js) will be part of the results path. It is recommended, to choose the file name unique.
    --dir               File system path to www folder of HolyGhost. Needed only if www folder has been moved to another location outside HolyGhost.
    --url               URL with trailing slash to www folder of HolyGhost. A webserver has to be configured to serve this url. If omitted, HolyGhost does not provide any URLs in its output.
    --html              Print html formatted links
    --proxy             Specify the proxy server to use, e.g. --proxy=192.168.1.42:8080
    --keep              Keep test results (screenshots and HAR files) on success
    --delete            Delete test results (screenshots and HAR files) even on error
    --debug             Enable debugging output
    --label             Use this string instead of "Total" as performance data label
    --param             Supply multiple optional parameters for use in your test cases.
                        Use them in your test cases with casper.cli.get("hgParam0"); ... casper.cli.get("hgParamN");

USAGE
    exit 0;
}

use strict;
use Getopt::Long;
use POSIX qw(strftime);
use File::Path qw(make_path remove_tree);
use XML::LibXML;
use File::Spec;
use File::Basename;
use File::Which;
use Time::HiRes qw(time);
my $scriptpath = dirname( File::Spec->rel2abs(__FILE__) );

# Extended die
sub die_rc { my $return_code = shift; die @_ }

#possible code for graphite sender
## Graphite sender - pass $case_name,%step as argument
#sub send_graphite
#{
#    # Format: HolyGhost.CASE_NAME.STEP_NAME Value Epochtime\n
#
#    #TODO: pass in arguments
#    
#    my $graphite_server = '127.0.0.1';
#    my $graphite_port = 2003;
#    
#    my $sock = IO::Socket::INET->new(
#            PeerAddr => $graphite_server,
#            PeerPort => $graphite_port,
#            Proto    => 'tcp'
#    );
#    
#    my $time = time;
#    $case_name =~ s/\./_/g;
#    $step_name =~ s/\./_/g;
#    #TODO: also replace spaces
#    foreach my $gstep ( sort keys %step ) {
#        $sock->send("HolyGhost.$case_name.$gstep $step{$gstep} $time\n") unless $opt{debug};
#        print "HolyGhost.$case_name.$gstep $step{$gstep} $time\n" if $opt{debug};
#    }
#    
#    $sock->shutdown(2);
#}



# Casper executable to use
my $casper_bin = which('casperjs');
if ( ! $casper_bin ) {
    die_rc( 3, "UNKNOWN: casperjs not found in path.\n" );
}

# Set base variables
my $pid = $$;
my $timestamp = ( strftime "%Y-%m-%d_%H-%M-%S", localtime );

# Prepare exit states
my %ERRORS = ( 'UNKNOWN', '3', 'OK', '0', 'WARNING', '1', 'CRITICAL', '2' );

# Parse options
my %opt;
my @params;
my @required_opts = qw(test);
GetOptions(
    "h|help"       => \&PrintUsage,
    "test=s"       => \$opt{test},
    "dir=s"        => \$opt{dir},
    "url=s"        => \$opt{url},
    "html"         => \$opt{html},
    "proxy:s"      => \$opt{proxy},
    "keep"         => \$opt{keep},
    "delete"       => \$opt{delete},
    "debug"        => \$opt{debug},
    "label=s"      => \$opt{label},
    "param=s"      => \@params,
);

# Parse required options
foreach (@required_opts) {
    if ( not defined $opt{$_} ) {
        die_rc( 3, "UNKNOWN: Required parameter --$_ not defined! See $0 -h for help.\n" );
    }
}

# Get testcase name
my $testcase = basename($opt{test}, ".js");

# Create test result directory
my $dest_dir;
if ( $opt{dir} ) {
    $dest_dir = $opt{dir} . '/results/' . $testcase . '/' . $timestamp . '_' . $pid;
} else {
    $dest_dir = $scriptpath . '/www/results/' . $testcase . '/' . $timestamp . '_' . $pid;
}
if ( !-e $dest_dir ) {
    make_path($dest_dir)
      or die_rc( 3, "UNKNOWN: Could not create directory $dest_dir - $!" );
}
# Results part for urls
my $results_url;
if ( $opt{url} ) {
    $results_url = 'results/' . $testcase . '/' . $timestamp . '_' . $pid .'/';
}
# Cookie-Jar
my $cookiejar = $dest_dir . '/cookiejar';
# Perfdata label
my $label;
if ( $opt{label} ) {
    $label = $opt{label};
}
else {
    $label = 'Total';
}

# Construct CasperJS command
my $casper_cmd =
    $casper_bin
  . ' --no-colors'
  . ' --ssl-protocol=tlsv1'
  . ' --web-security=no'
  . ' --ignore-ssl-errors=true'
  . ' --pre=' . $scriptpath . '/HolyGhost.js'
  . ' --hgResultpath=' . $dest_dir
  . ' --cookies-file=' . $cookiejar;
if ( $opt{proxy} ) {
    $casper_cmd .= ' --proxy-type=http --proxy=' . $opt{proxy};
}
else {
    $casper_cmd .= ' --proxy-type=none';
}
if ( $opt{debug} ) {
    $casper_cmd .= ' --verbose --log-level=debug --hgDebug';
}
if (@params) {
    my $cnt = 0;
    foreach my $param (@params) {
        $casper_cmd .= ' --hgParam' . $cnt . '="' . $param . '"';
    }
}
$casper_cmd .= ' --xunit=\'' . $dest_dir . '/xunit.log\' test ' . $opt{test};

if ( $opt{debug} ) {
    print "CASPER CALL:\n";
    print $casper_cmd . "\n";
}

my $start    = time;
my @output   = `$casper_cmd`;
my $rc       = ( $? >> 8 );
my $duration =  sprintf (" %.3f", (time - $start) );
my @detailed = grep ( /^PASS|^FAIL|^#\s{2,}/, @output );
s/[\|]/_/g for @detailed; # Replace unsafe chars

if ( $opt{debug} ) {
    print "CASPER OUTPUT:\n";
    print join("", @output);
    print "CASPER RETURN CODE:\n";
    print $rc . "\n";
    print "CASPER RUNTIME:\n";
    print $duration . "\n";
    print "DETAILED NAGIOS MESSAGE:\n";
    print join("", @detailed);
}

# Parse xunit log
my %steps;
my $parser = XML::LibXML->new();
my $root   = $parser->parse_file( $dest_dir . '/xunit.log' )
  or
  die_rc( 3, "UNKNOWN: Cannot open " . $dest_dir . "/xunit.log for parsing\n" );

# Note:
# A HolyGhost-"Case" consist of exactly one xunit-"Testsuite", only the first Testsuite will be parsed.
# A Holyghost-"Step" consists of one or more following xunit-"Testcases".
my @cases = $root->findnodes('/testsuites/testsuite');
my $case  = shift(@cases);

my $case_name = $case->getAttribute("name");
my ( $case_state, $case_shortname, $case_desc ) = split( /::/, $case_name );
unless ( $case_state =~ /^OK$|^WARNING$|^ERROR$|^CRITICAL$|^UNKNOWN$/ ) {
    $case_state = "UNKNOWN";
}
my $case_time = $case->getAttribute("time");

s/$case_name/$case_shortname ( $case_desc )/ for @detailed;

my %step;
TESTCASE: foreach my $step (
    $root->findnodes(
        '/testsuites/testsuite[@name="' . $case_name . '"]/testcase'
    )
  )
{
    my $step_name = $step->getAttribute("name");
    next if $step_name =~ m/Test.ran.into/;
    my ( $step_shortname, $step_desc ) = split( /::/, $step_name );
    $step{$step_shortname} += $step->getAttribute("time");
}

# Format perfdata
my $perfdata = "|";
foreach my $pstep ( sort keys %step ) {
    $perfdata .= "\'$pstep\'=" . $step{$pstep} . "s ";
}
$perfdata .= "\'" . $label . "\'=" . $case_time . "s ";
$perfdata .= "\'CasperJSRuntime\'=" . $duration . "s ";

# Format plugin output
my $output       = "";
# TODO my $addtl_output = "";
if ( $rc == 0 ) {
    $output = "OK: $case_shortname ( $case_desc ) passed." . $perfdata . "\n";
}
else {
    $output =
      "$case_state: $case_shortname ( $case_desc ) failed." . $perfdata . "\n";
    $rc = $ERRORS{$case_state};

    # TODO add optional parsers here
    # TODO read JSESSIONID if any
    # TODO open( HARFILE, "<" . $dest_dir . "/har.har" );
    # TODO my $har = join( "", <HARFILE> );
    # TODO my @jsessionids = $har =~ /(JSESSIONID=[^;]+)\;/g;
    # TODO $addtl_output = "\n" . join( "\n", @jsessionids ) . "\n";
}
print $output;
# TODO print $addtl_output;
print @detailed;

# Print URLs
if ( ! $opt{delete}) {
    if ( $opt{url} ) {
        if ( $opt{keep} || $rc != 0 ) {
	    if ( $opt{html} ) {
                print "\n" . '<a href="' . $opt{url} . 'viewer.html?path=' . $results_url . 'har.har">Waterfall chart ';
                print "( " . $opt{url} . "viewer.html?path=" . $results_url . "har.har" . " )</a>\n";
                print "\n" . '<a href="' . $opt{url} . $results_url . '">Recorded assets ';
                print "( " . $opt{url} . $results_url . " )</a>\n";
		print "\n";
	    } else {
                print "\nWaterfall chart:\n";
                print $opt{url} . "viewer.html?path=" . $results_url . "har.har" . "\n";
                print "\nRecorded assets:\n";
                print $opt{url} . $results_url . "\n\n";
	    }
        }
    }
}

print "CasperJS total runtime " . $duration . " sec.\n";

# Remove results on test success if "keep" is not desired
unless ( $opt{keep} ) {
    if ( $rc == 0 ) {
        remove_tree($dest_dir) or print $!;
    }
}

# Remove results in any case if "delete" is desired
if ( $opt{delete} ) {
    remove_tree($dest_dir) or print $!;
}

exit $rc;

